import "listener" as listener
import "logger" as *
import "static" as *
import "cookie" as *
import "formdata" as *
import "debug" as __debug

let setDebug(value) = do
    if (__debug.inDebug == value) return
    __debug.inDebug = value
    if (value) do
        term.log("Debug mode enabled")
    else do
        term.log("Debug mode disabled")

let server() = do
    let class = {port: 3000, numThreads: 1, callback: ()=term.log("Server running on port "+class.port)}
    let threads = []
    let routes = []
    let Socket = null
    class.onErr(e) = do
        term.error(e.type+": "+e.message)
        term.error(e.code)
        term.error(e.path+" on line "+e.line)
    class.run() = do
        if (Socket != null) do
            throwError("Server already running")
        Socket = socket.server("tcp", class.port)
        let starter() = do
            for (i from 0 to class.numThreads) do
                thread(()=listener.listener(Socket, routes, class.onErr)).start()
            class.callback()
        let t = thread(starter)
        t.start()
    
    class.close() = Socket.close()
        
    class.route(path) = do
        let class = {}
        class.get(callback) = do
            routes.append({path: path, callback: callback, methods: ["GET"], type: "route"})
            return class
        class.post(callback) = do
            routes.append({path: path, callback: callback, methods: ["POST"], type: "route"})
            return class
        class.put(callback) = do
            routes.append({path: path, callback: callback, methods: ["PUT"], type: "route"})
            return class
        class.delete(callback) = do
            routes.append({path: path, callback: callback, methods: ["DELETE"], type: "route"})
            return class
        class.all(callback) = do
            routes.append({path: path, callback: callback, methods: true, type: "route"})
            return class
        return class
    class.get(path, callback) = class.route(path).get(callback)
    class.post(path, callback) = class.route(path).post(callback)
    class.put(path, callback) = class.route(path).put(callback)
    class.all(path, callback) = class.route(path).all(callback)
    class.use(callback) = routes.append({callback: callback, type: "middleware"})
    return class